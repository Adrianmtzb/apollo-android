subprojects {
  apply from: rootProject.file("gradle/dependencies.gradle")

  buildscript {
    repositories {
      jcenter()
      maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    dependencies {
      classpath dep.kotlinGradlePlugin
    }
  }
  repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  group = GROUP
  version = VERSION_NAME

  if (!project.name.equals('apollo-gradle-plugin')) {
    apply plugin: 'checkstyle'

    checkstyle {
      configFile rootProject.file('checkstyle.xml')
      configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
      ignoreFailures false
      showViolations true
    }

    task checkstyle(type: Checkstyle) {
      source 'src/main/java'
      include '**/*.java'

      classpath = files()
    }

    afterEvaluate { project ->
      if (project.tasks.findByName('check')) {
        check.dependsOn('checkstyle')
      }
      if (project.hasProperty("android")) {
        android.libraryVariants.all { variant ->
          tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
          }
        }
      }
    }
  }
}
